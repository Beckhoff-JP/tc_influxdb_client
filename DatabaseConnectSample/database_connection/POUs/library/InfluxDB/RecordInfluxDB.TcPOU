<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="RecordInfluxDB" Id="{8024213b-1b7f-44f6-b63f-23d9e46d68db}" SpecialFunc="None">
    <Declaration><![CDATA[(*
Insert dedicated driver for InfluxDB.

:Author: 

	Takashi Ichihashi Beckhoff Automation K.K.
	
:Date:

	2023-03-09
	
This function block is based on sample code published on infosys sites. 
Link:  `https://infosys.beckhoff.com/content/1033/tf6420_tc3_database_server/8117583755.html?id=5199556541044360835 <https://infosys.beckhoff.com/content/1033/tf6420_tc3_database_server/8117583755.html?id=5199556541044360835>`

	
*)

FUNCTION_BLOCK RecordInfluxDB IMPLEMENTS IRecordDB

VAR_OUTPUT
	bBusy			: BOOL;	// In writing database
	bError			: BOOL; // Somthing wrong situation happend
	TcResult		: Tc3_Database.I_TcMessage; // Response message
    sErrorMessage	: STRING(255);	// Error message
END_VAR
VAR
	QueryOption_TSDB_Insert 			: T_QueryOptionTimeSeriesDB_Insert; // defines detailed Queryparameter
    fbNoSQLQueryBuilder_TimeSeriesDB 	: FB_NoSQLQueryBuilder_TimeSeriesDB; // defines database type specific api
    fbNoSqlQueryEvt 					: FB_NoSQLQueryEvt(sNetID := '', tTimeout := T#15S); // functionblock to execute queries
	
	// Variables for queue feature
	get_data 			: ChunkCommand;
	State				: E_DbLogState;
	_dbid				: UDINT;
	_queue : dequeue;	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE State OF

	E_DbLogState.idle:
		
		IF NOT fbNoSqlQueryEvt.bBusy THEN
			State := E_DbLogState.init;
		END_IF

	E_DbLogState.init:
	
		IF NOT fbNoSqlQueryEvt.bBusy AND _queue.queue_usage > 0 THEN
			// getting data
			get_data := _queue.get();
			// Data set
			IF get_data.chunk_size > 0 THEN
				fbNoSQLQueryBuilder_TimeSeriesDB.pQueryOptions := ADR(QueryOption_TSDB_Insert);
				fbNoSQLQueryBuilder_TimeSeriesDB.cbQueryOptions := SIZEOF(QueryOption_TSDB_Insert);    
				QueryOption_TSDB_Insert.sTableName := get_data.sql_parameters.table_name;
				QueryOption_TSDB_Insert.sDataType := get_data.structure_name;
				QueryOption_TSDB_Insert.pSymbol := get_data.start_pointer;
				QueryOption_TSDB_Insert.cbSymbol := get_data.structure_size * get_data.chunk_size;
				QueryOption_TSDB_Insert.nDataCount := get_data.chunk_size;
				QueryOption_TSDB_Insert.nStartTimestamp := get_data.start_time;
				QueryOption_TSDB_Insert.nCycleTime := get_data.cycle_time_100ns; // (in 100 ns)
				State := E_DbLogState.writing;
			END_IF
		
		END_IF

	E_DbLogState.writing:
	
		IF fbNoSqlQueryEvt.Execute(_dbid, fbNoSQLQueryBuilder_TimeSeriesDB) THEN
			IF fbNoSqlQueryEvt.bError THEN
				TcResult := fbNoSqlQueryEvt.ipTcResult;                
				State := E_DbLogState.error;
			ELSE		
				State := E_DbLogState.idle;
				bError := FALSE;
			END_IF
		END_IF

	E_DbLogState.error:
	
		IF TcResult.RequestEventText(1033, sErrorMessage, SIZEOF(sErrorMessage)) THEN
            TcResult.Send(F_GetSystemTime());
            State := E_DbLogState.idle;
            bError := TRUE;
        END_IF
END_CASE
]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{4e7c5051-b1cc-423d-9c05-60b0eab25d65}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	DBID		: UDINT; //Database ID for TF6420 configurator
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_dbid := DBID;]]></ST>
      </Implementation>
    </Method>
    <Property Name="queue" Id="{9e0e16f0-3d31-4150-b701-80b92abe9211}">
      <Declaration><![CDATA[(*Property of database command queue*)
{warning 'add property implementation'}
PROPERTY queue : REFERENCE TO dequeue
]]></Declaration>
      <Get Name="Get" Id="{67bb3399-7d3e-42aa-a50d-532cc78f00af}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[queue REF= _queue;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{ef2eb95e-01f5-40e3-b3c1-f5f9552f7029}">
        <Declaration><![CDATA[
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_queue := queue;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="RecordInfluxDB">
      <LineId Id="2928" Count="48" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="RecordInfluxDB.FB_init">
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="RecordInfluxDB.queue.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="RecordInfluxDB.queue.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>